apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }
    def Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))
    def code = (versionProps['VERSION_CODE'] ?: "34").toInteger() + 1
    def buildNumber = (versionProps['BUILD_NUMBER'] ?: "0").toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps['BUILD_NUMBER'] = buildNumber.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
            applicationId "com.tokko.cameandwent.cameandwent"
            multiDexEnabled = true
            minSdkVersion 19
            targetSdkVersion 22
            versionCode code
            versionName "1.3."+buildNumber
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
        }
        mock {
            applicationIdSuffix ".mock"
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    serviceAccountEmail = '92588905880-fdolvkj29p3l637u5fgaa3jke0vksd17@developer.gserviceaccount.com'
    pk12File = file('key.p12')
    //   uploadImages = true
}

retrolambda {
    include 'Test', 'Debug', 'Release'
    oldJdk = System.getenv("JAVA7_HOME")
    javaVersion = JavaVersion.VERSION_1_6
}
dependencies {
    repositories {
        mavenCentral()
    }
    compile 'joda-time:joda-time:2.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0-rc2'
    testCompile "org.mockito:mockito-core:1.10.0"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.annimon:stream:1.0.1'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'

    compile('org.simpleframework:simple-xml:2.7.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
}
